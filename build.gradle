plugins {
    id "com.netflix.nebula.node" version "3.0.0" apply false
    alias(libs.plugins.com.github.ben.manes.versions)
    alias(libs.plugins.org.cyclonedx.bom)
    alias(libs.plugins.org.sonarqube)
    alias(libs.plugins.org.springframework.boot)
}

ext {
    infraVersion = System.properties['infraVersion'] ?: '4.0.0-SNAPSHOT'

    randomBeansVersion = "3.9.0"
    nodeVersion = "18.20.0"
    npmVersion = "10.8.2"

    ineraMavenRepository = 'https://nexus.drift.inera.se/repository/it-public/'
}

apply from: 'gradle/configureIntygBom.gradle'

springBoot {
    mainClass = 'se.inera.intyg.intygsadmin.web.IntygsadminApplication'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'test-report-aggregation'

    group = 'se.inera.intyg.intygsadmin'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    test { useJUnitPlatform() }

    repositories {
        mavenLocal()
        maven { url "${ineraMavenRepository}" }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'jacoco'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion)
        }
    }

    configurations {
        mockitoAgent
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-parameters")
    }
    compileJava.dependsOn processResources

    dependencies {
        implementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        annotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testImplementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testAnnotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        mockitoAgent platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        mockitoAgent("org.mockito:mockito-core") { transitive = false }
    }

    test {
        jvmArgs += [
                "-javaagent:${configurations.mockitoAgent.asPath}",
                "-Xshare:off"
        ]
    }

    testing {
        suites {
            test {
                useJUnitJupiter()
            }
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**",
                ])
            }))
        }
    }

    tasks.matching { it.name == "cyclonedxDirectBom" }.configureEach { t ->
        t.includeConfigs = ['^runtimeClasspath$']
    }

    tasks.withType(Test).configureEach {
        jvmArgs '-Xshare:off'
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-intygsadmin"
        property "sonar.projectName", "intyg-intygsadmin"
        property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.json" as String
        property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.html" as String
    }
}

reporting {
    subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
        dependencies { testReportAggregation project(":${subprojectName}") }
    }
}