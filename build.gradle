plugins {
    id "io.spring.dependency-management" version "1.1.6" apply false
    id "com.netflix.nebula.node" version "3.0.0" apply false
    id 'org.springframework.boot' version '3.3.5'
    id 'org.sonarqube' version '5.1.0.4882'
    id "org.owasp.dependencycheck" version "10.0.4"
    id 'test-report-aggregation'
    id 'jacoco'
}

ext {
    infraVersion = System.properties['infraVersion'] ?: '4.0.0-SNAPSHOT'
    apachePoiVersion = "5.3.0"
    commonsIOVersion = "2.18.0"
    guavaVersion = "33.3.1-jre"
    logbackEcsEncoderVersion = '1.6.0'
    mapstructVersion = "1.6.3"
    picocliVersion = "4.7.6"
    shedlockSpringVersion = "5.16.0"
    springDocVersion = "2.6.0"
    randomBeansVersion = "3.9.0"

    nodeVersion = "12.22.12"
    npmVersion = "6.14.16"

    ineraMavenRepository = 'https://nexus.drift.inera.se/repository/it-public/'

    springbootMavenBomVersion = "3.3.5"

    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

allprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: "application"
    apply plugin: 'java'
    apply plugin: "idea"
    apply plugin: 'jacoco'

    mainClassName = 'se.inera.intyg.intygsadmin.web.IntygsadminApplication'
    group = 'se.inera.intyg.intygsadmin'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'
    sourceCompatibility = 21
    targetCompatibility = 21

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    dependencies {
        implementation "org.apache.commons:commons-lang3"
        implementation "com.google.guava:guava:${guavaVersion}"

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        testImplementation "io.github.benas:random-beans:${randomBeansVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.platform:junit-platform-runner"
        testImplementation "org.mockito:mockito-core"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.springframework.security:spring-security-test"

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    test { useJUnitPlatform() }

    repositories {
        mavenLocal()
        maven { url "${ineraMavenRepository}" }
        mavenCentral()
    }
}


jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/testability/**",
                    "**/integrationtest/**"
            ])
        }))
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-intygsadmin"
        property "sonar.projectName", "intyg-intygsadmin"
        property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.json" as String
        property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.html" as String
    }
}

dependencyCheck {
    formats = List.of("HTML", "JSON")
    nvd.setDatafeedUrl(nvdMirror)
    analyzers.setAssemblyEnabled(false)
}

reporting {
    dependencies {
        testReportAggregation project(':intygsadmin-web')
        testReportAggregation project(':intygsadmin-persistence')
    }
}
